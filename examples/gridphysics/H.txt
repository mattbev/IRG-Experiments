BasicGame square_size=40
    SpriteSet

        floor > Immovable img=oryx/backBiege
        base    > Immovable    color=WHITE img=newset/camel1
        avatar  > ShootAvatar stype=bullet img=newset/sheriff1
        surround > Immovable    color=YELLOW img=dirt invisible=True
        bullet > Missile img=newset/girl2 shrinkfactor=0.5 singleton=True

        turn > Immovable invisible=True hidden=True
            turnup > orientation=UP img=dirt
            turndown > orientation=DOWN img=dirt
            turnleft > orientation=LEFT img=dirt
            turnright > orientation=RIGHT img=dirt

        bandit > Bomber prob=0.1 cooldown=6 speed=1 
            bup > orientation=UP spawnorientation=LEFT stype=missile img=newset/tooth1
            bdown > orientation=DOWN spawnorientation=RIGHT stype=missile img=newset/tooth2
            bleft > orientation=LEFT spawnorientation=DOWN stype=missile img=newset/tooth3
            bright > orientation=RIGHT spawnorientation=UP stype=missile img=newset/tooth4

        missile > Missile img=newset/fastfoodtrolley1 shrinkfactor=0.5 speed=0.6

        wall > Immovable autotiling=True img=oryx/dirtwall

    LevelMapping
        0 > floor base
        u > floor surround bup
        d > floor surround bdown
        l > floor surround bleft
        r > floor surround bright
        s > floor surround
        A > floor avatar
        1 > floor turnup
        2 > floor turnleft
        3 > floor turndown
        4 > floor turnright
        w > floor wall
        . > floor

    TerminationSet
        SpriteCounter      stype=avatar             limit=0 win=False
        SpriteCounter      stype=bandit             limit=0 win=True
        Timeout limit=1000 win=True
        
    InteractionSet
        avatar surround base wall > stepBack

        base missile > killBoth
        base bullet > killBoth scoreChange=-1
        EOS missile bullet > killSprite

        bleft turndown > transformTo stype=bdown
        bright turnup > transformTo stype=bup
        bdown turnright > transformTo stype=bright
        bup turnleft > transformTo stype=bleft

        bandit bullet > killBoth scoreChange=5
        avatar missile > killBoth scoreChange=-5

        bullet wall > killSprite