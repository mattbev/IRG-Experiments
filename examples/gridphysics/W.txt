BasicGame square_size=40
    SpriteSet

        background > Immovable img=oryx/space1 hidden=True
        asteroid > Immovable img=newset/ladder1

        missile > Missile
            sam  > orientation=RIGHT color=BLUE speed=1.0 img=newset/painter1 shrinkfactor=0.5
            laser > orientation=LEFT speed=0.3 color=RED shrinkfactor=0.75 img=newset/potatoeyes

        portal  >
            portalSlow  > SpawnPoint   stype=alien  cooldown=10 prob=0.05 img=newset/snowmanbase portal=True

        shield > Resource color=GOLD limit=4 img=newset/nurse

        avatar  > ShootAvatar color=YELLOW stype=sam speed=1.0 img=newset/car_red rotateInPlace=False
        alien > Bomber color=BROWN img=oryx/bush1 speed=0.1 orientation=LEFT stype=laser prob=0.01
    
    LevelMapping
        1 > background portalSlow
        A > background avatar shield
        s > background asteroid
        . > background

    TerminationSet
        SpriteCounter      stype=avatar             limit=0 win=False
        Timeout limit=1000 win=True
        
    InteractionSet
        avatar  EOS  > stepBack
        alien   EOS  > killSprite
        missile EOS  > killSprite

        alien sam > killBoth scoreChange=5

        sam laser > transformTo stype=shield killSecond=True

        avatar shield > changeResource resource=shield value=1 killResource=True

        avatar alien > killIfHasLess resource=shield limit=0 scoreChange=-5
        avatar alien > changeResource resource=shield value=-1 killResource=True

        avatar laser > killIfHasLess resource=shield limit=0 scoreChange=-5
        avatar laser > changeResource resource=shield value=-1 killResource=True

        asteroid laser > killSprite
        asteroid sam > killSprite scoreChange=-1
        alien asteroid > killSprite
        laser asteroid > killSprite
        avatar asteroid > stepBack